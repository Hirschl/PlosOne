LIBNAME CAH "U:\1Projects\PSID\WAVE37\SOURCEFILES\cah85_11";
LIBNAME PSID "U:\1Projects\PSID\WAVE37\WORKFILES";



%LET YEAR = 1968	1969	1970	1971	1972	1973	1974	1975	1976	1977	1978	1979	1980	1981	1982	1983	1984	1985	1986	1987	1988	1989	1990	1991	1992	1993	1994	1995	1996	1997	1999	2001	2003	2005	2007	2009 2011;
%LET FAM  = V3		V442	V1102	V1802	V2402	V3002	V3402	V3802	V4302	V5202	V5702	V6302	V6902	V7502	V8202	V8802	V10002	V11102	V12502	V13702	V14802	V16302	V17702	V19002	V20302	V21602	ER2002	ER5002	ER7002	ER10002	ER13002	ER17002	ER21002	ER25002	ER36002	ER42002 ER47302;
%LET INDA = ER30001	ER30020	ER30043	ER30067	ER30091	ER30117	ER30138	ER30160	ER30188	ER30217	ER30246	ER30283	ER30313	ER30343	ER30373	ER30399	ER30429	ER30463	ER30498	ER30535	ER30570	ER30606	ER30642	ER30689	ER30733	ER30806	ER33101	ER33201	ER33301	ER33401	ER33501	ER33601	ER33701	ER33801	ER33901	ER34001 ER34101;
%LET INDB = ER30002	ER30021	ER30044	ER30068	ER30092	ER30118	ER30139	ER30161	ER30189	ER30218	ER30247	ER30284	ER30314	ER30344	ER30374	ER30400	ER30430	ER30464	ER30499	ER30536	ER30571	ER30607	ER30643	ER30690	ER30734	ER30807	ER33102	ER33202	ER33302	ER33402	ER33502	ER33602	ER33702	ER33802	ER33902	ER34002 ER34102;

%MACRO MYLIB;
%DO J = 1 %TO 37 ;
%LET I = %SCAN(&YEAR,&J) ;

LIBNAME FAM&I "U:\1Projects\PSID\WAVE37\SOURCEFILES\&I\FAM&I";
LIBNAME IND&I "U:\1Projects\PSID\WAVE37\SOURCEFILES\&I\IND&I.ER";
LIBNAME FI&I "U:\1Projects\PSID\WAVE37\SOURCEFILES\&I\FI&I";

%END;
%MEND MYLIB;
%MYLIB;

%MACRO FAMINDIV;

/*In the family file, create a variable equal to the family interview number.  Name this new variable after its equivalent in the individual file.
For example: ER30020 = V442  where ER30020 is the family interview number in the 1969 INDIVIDUAL file, while V442 is the family interview number in the 1969 FAMILY file.
You need to create this variable (eg., ER30020 example above) so that you can use this variable, which is now in the family file, to match with its corresponding individual file.
*/

%DO I = 1 %TO 37;
DATA FAM%SCAN(&YEAR,&I).FAM%SCAN(&YEAR,&I);
SET FAM%SCAN(&YEAR,&I).FAM%SCAN(&YEAR,&I);
%SCAN(&INDA,&I) = %SCAN(&FAM,&I);
RUN;

/*sort the family and individual datasets in preparation for merging*/
PROC SORT DATA = FAM%SCAN(&YEAR,&I).FAM%SCAN(&YEAR,&I);
BY %SCAN(&INDA,&I);
RUN;

PROC SORT DATA = IND%SCAN(&YEAR,&I).IND%SCAN(&YEAR,&I)ER;
BY %SCAN(&INDA,&I) %SCAN(&INDB,&I);
RUN;

/*merge the individual and family datasets for each wave by family interview number for that wave*/
*SAVE THE COMBINDED FILE IN THE FI (FAMILY INDIVIDUAL) FOLDER;

DATA FI%SCAN(&YEAR,&I).FAMILYINDIV%SCAN(&YEAR,&I);
MERGE FAM%SCAN(&YEAR,&I).FAM%SCAN(&YEAR,&I) IND%SCAN(&YEAR,&I).IND%SCAN(&YEAR,&I)ER;
BY %SCAN(&INDA,&I);
RUN;

/*remember, each individual file has er30001 and er30002, so you can sort the data using both as sort by variables.
This is in preparation for merging all 37 WAVES of combined PSID family and individual data*/

PROC SORT DATA = FI%SCAN(&YEAR,&I).FAMILYINDIV%SCAN(&YEAR,&I);
BY  ER30001 ER30002;
RUN;

%END;
%MEND FAMINDIV;
%FAMINDIV;

*SORT THE IND2011SUMMARY FILE BECAUSE THAT WILL MERGED WITH THE COMBINED FILE LATER AND IT CONTAINS THE SUMMARY
VARIABLES FOR ALL CASES IN THE PSID DATASET;

PROC SORT DATA = IND1968.IND2011SUMMARY ;
BY ER30001 ER30002  ;
RUN;






