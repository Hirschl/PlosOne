*STEP 10 - CREATE COVARIATES AT AGE 55;

%LET SY = 55;

LIBNAME PSID "U:\1Projects\PSID\WAVE37\WORKFILES";
LIBNAME PSIDLT&SY "U:\1Projects\PSID\WAVE37\LIFETABLES&SY";

%MACRO COVAR;

DATA PSIDLT&SY..COVARIATESAT&SY
(KEEP =
V1 
V3 
ER30001 
ER30002
INDIVYEAROFBIRTH
HYBRID_INDIVYEAROFBIRTH
ENTRYYEAR
CALCAGE
HYBRID_CALCAGE
SEX
INDIVRACE
RACE
RELTOHEAD
RELTOHEAD_DETAILED
NUMINFU 	
TOTFAMINC 	
MARSTAT
AGE	
DISABILITY	
EDUC
RACEA		
RACEB		
RACEC		
RACED		
RACEAW		
RACEBW		
RACECW		
RACEDW	
RACEINDIV	
REGION		
OCCUPATIONA
OCCUPATIONWA
COREINDIV_WT	
COREINDIV_CROSSSECTWT
LATINOINDIV_WT
);
SET PSID.HOUSEHOLDHEADSELECTEDVARSYBPTW;
ENTRYYEAR = HYBRID_INDIVYEAROFBIRTH + &SY;
SEX = ER32000;
RACE = RACEA1968;
RACEA= RACEA1968;

CALCAGE = ENTRYYEAR-INDIVYEAROFBIRTH;
HYBRID_CALCAGE = HYBRID_INDIVYEAROFBIRTH + &SY - HYBRID_INDIVYEAROFBIRTH;

IF 3001<=ER30001<=3441 THEN 
	DO;
		RACE  = RACEA1997;
		RACEA = RACEA1997;
		RACEB = RACEB1997;
		RACEC = RACEC1997;
		RACED = RACED1997;
		RACEAW= RACEAW1997;
		RACEBW= RACEBW1997;
		RACECW= RACECW1997;
		RACEDW= RACEDW1997;
	END;
ELSE IF 3442<=ER30001<=3511 THEN 
	DO;
		RACE  = RACEA1999;
		RACEA = RACEA1999;
		RACEB = RACEB1999;
		RACEC = RACEC1999;
		RACED = RACED1999;
		RACEAW= RACEAW1999;
		RACEBW= RACEBW1999;
		RACECW= RACECW1999;
		RACEDW= RACEDW1999;
	END;
ELSE IF 7001<=ER30001<=9043 THEN 
	DO;
		RACE  = RACEA1990;
		RACEA = RACEA1990;
		RACEB = RACEB1990;
		RACEAW= RACEAW1990;
		RACEBW= RACEBW1990;
	END;
ELSE IF 9044<=ER30001<=9308 THEN 
	DO;
		RACE  = RACEA1992;
		RACEA = RACEA1992;
		RACEB = RACEB1992;
		RACEAW= RACEAW1992;
		RACEBW= RACEBW1992; 
	END;

%DO I = 1968 %TO 1982;
	 IF ENTRYYEAR = &I AND RELTOHEAD&I  = 1 THEN RELTOHEAD = 1;
ELSE IF ENTRYYEAR = &I AND RELTOHEAD&I NE 1 THEN RELTOHEAD = 0;
	 IF ENTRYYEAR = &I THEN RELTOHEAD_DETAILED = RELTOHEAD&I;
%END;

%DO I = 1983 %TO 1997;
	 IF ENTRYYEAR = &I AND RELTOHEAD&I  = 10 THEN RELTOHEAD = 1;
ELSE IF ENTRYYEAR = &I AND RELTOHEAD&I NE 10 THEN RELTOHEAD = 0;
	 IF ENTRYYEAR = &I THEN RELTOHEAD_DETAILED = RELTOHEAD&I;
%END;

%DO I = 1999 %TO 2011 %BY 2; 
%LET J = %EVAL(&I-1);
	 IF ENTRYYEAR IN (&I, &J) AND RELTOHEAD&I  = 10 THEN RELTOHEAD = 1;
ELSE IF ENTRYYEAR IN (&I, &J) AND RELTOHEAD&I NE 10 THEN RELTOHEAD = 0;
	 IF ENTRYYEAR IN (&I, &J) THEN RELTOHEAD_DETAILED = RELTOHEAD&I;
%END;

%DO I = 1968 %TO 1997;
IF ENTRYYEAR = &I THEN DO;
NUMINFU 	= NUMINFU&I;
TOTFAMINC 	= TOTFAMINC&I;
MARSTAT		= MARSTAT&I;
AGE			= AGE&I;
DISABILITY	= DISABILITY&I;
EDUC		= EDUC&I;
REGION		= REGION&I;
OCCUPATIONA	= OCCUPATIONA&I;
OCCUPATIONWA= OCCUPATIONWA&I;
COREINDIV_WT= COREINDIV_WT&I;
COREINDIV_CROSSSECTWT=COREINDIV_CROSSSECTWT&I;
LATINOINDIV_WT=LATINOINDIV_WT&I;
END;
%END;

%DO I = 1999 %TO 2011 %BY 2; 
%LET J = %EVAL(&I-1);
IF ENTRYYEAR IN (&I, &J) THEN DO;
NUMINFU 	= NUMINFU&I;
TOTFAMINC 	= TOTFAMINC&I;
MARSTAT		= MARSTAT&I;
AGE			= AGE&I;
DISABILITY	= DISABILITY&I;
EDUC		= EDUC&I;
REGION		= REGION&I;
OCCUPATIONA	= OCCUPATIONA&I;
OCCUPATIONWA= OCCUPATIONWA&I;
COREINDIV_WT= COREINDIV_WT&I;
COREINDIV_CROSSSECTWT=COREINDIV_CROSSSECTWT&I;
END;
%END;

RACEINDIV = RACEI1;
IF RACEI1 IN (.,8) THEN RACEINDIV = RACE;

RUN;
%MEND COVAR;
%COVAR;


DATA PSIDLT&SY..COVARIATESAT&SY;
SET PSIDLT&SY..COVARIATESAT&SY;
IF ENTRYYEAR GE 1968 AND ENTRYYEAR LE 2011;
/*REMOVE LATINOS*/
IF ER30001 < 7000;
RUN;
/*
NOTE: There were 73140 observations read from the data set PSIDLT25.COVARIATESAT25.
NOTE: The data set PSIDLT25.COVARIATESAT25 has 39041 observations and 35 variables
*/

PROC SORT DATA = PSIDLT&SY..COVARIATESAT&SY;
BY ER30001 ER30002 ;
RUN;
/* BEFORE: 31,692
NOTE: There were 39041 observations read from the data set PSIDLT25.COVARIATESAT25.
NOTE: The data set PSIDLT25.COVARIATESAT25 has 39041 observations and 35 variables.
*/

PROC FREQ DATA = PSIDLT&SY..COVARIATESAT&SY;
TABLE ENTRYYEAR INDIVRACE RACEA RACEINDIV ;
RUN;
